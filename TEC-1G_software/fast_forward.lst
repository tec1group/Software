0000                             ;Fast Forward by J Robinson
0000                             ; 
0000                             ;Modified by Brian Chiha to work on all MONS provided you have the 4k7 NMI to D7 hardware fix
0000                             ;Set HL on line 0F01-02 to your start address
0000                             ;Enjoy...
0000                             ; 
1000                          .ORG   1000H   
1000                SETUP:       
1000   21 00 00               LD   HL,0000H   ;Put start address to look at here
1003   AF                     XOR   A   ;Initial Key = 0
1004   F5                     PUSH   AF   ;Save Previous key
1005                START:       
1005   CD 70 10               CALL   CON_HL_A   ;Convert HL and its contents to screen display
1008   CD 90 10               CALL   KEY_TONE   ;Play a Beep
100B   11 00 04               LD   DE,0400H   ;Default Speed
100E   DB 03                  IN   A,(03)   ;Check if key is pressed
1010   CB 77                  BIT   6,A   
1012   20 3D                  JR   NZ,GET_PREV   ;No key pressed
1014   DB 00                  IN   A,(00)   ;Get actual key
1016   E6 1F                  AND   1FH   ;Mask upper bits
1018                CHK_INP:      ;Check input if any
1018   FE 0A                  CP   0AH   ;Fast Foward
101A   C2 23 10               JP   NZ,FASTR   ;Check Fast Reverse
101D   11 00 01               LD   DE,0100H   ;Go Forward fast
1020   23                     INC   HL   
1021   18 1E                  JR   DO_SCAN   
1023                FASTR:       
1023   FE 0B                  CP   0BH   ;Fast Reverse
1025   C2 2E 10               JP   NZ,SLOWR   ;Check Slow Reverse
1028   11 00 01               LD   DE,0100H   ;Go Backward fast
102B   2B                     DEC   HL   
102C   18 13                  JR   DO_SCAN   
102E                SLOWR:       
102E   FE 0D                  CP   0DH   ;Slow Reverse
1030   C2 39 10               JP   NZ,STOP   ;Check Stop
1033   11 00 04               LD   DE,0400H   ;Go Backward slow
1036   2B                     DEC   HL   
1037   18 08                  JR   DO_SCAN   
1039                STOP:        
1039   FE 0C                  CP   0CH   ;Stop
103B   C2 40 10               JP   NZ,SLOWF   ;Just move forward
103E   18 01                  JR   DO_SCAN   
1040                SLOWF:       
1040   23                     INC   HL   ;Move default speed forward
1041                DO_SCAN:      
1041   47                     LD   B,A   ;Save current key
1042   F1                     POP   AF   
1043   78                     LD   A,B   
1044   F5                     PUSH   AF   
1045                             ;LD     DE,(SPEED)  ;Display HL and A with Speed delay
1045   E5                     PUSH   HL   
1046                RESCAN:      
1046   CD 55 10               CALL   SCAN   ;Display the screen
1049   1B                     DEC   DE   
104A   7A                     LD   A,D   
104B   B3                     OR   E   
104C   20 F8                  JR   NZ,RESCAN   
104E   E1                     POP   HL   
104F   18 B4                  JR   START   
1051                GET_PREV:      ;Retrive the prevoius key and re save it
1051   F1                     POP   AF   
1052   F5                     PUSH   AF   
1053   18 C3                  JR   CHK_INP   
1055                SCAN:        ;Multiplex the displays
1055   06 20                  LD   B,20H   ;Segment Reference
1057   21 00 50               LD   HL,DISP_BUFF   ;Set HL to Display Buffer
105A                SCAN_LOOP:      
105A   7E                     LD   A,(HL)   ;Get Segment Value at HL
105B   D3 02                  OUT   (02),A   ;Set on Segment
105D   78                     LD   A,B   ;Get Segment reference
105E   D3 01                  OUT   (01),A   ;Activate segment
1060   06 80                  LD   B,80H   ;Segment delay
1062   10 FE        D_LOOP:   DJNZ   D_LOOP   
1064   23                     INC   HL   ;move to next location
1065   47                     LD   B,A   ;Save Segment reference
1066   AF                     XOR   A   ;Clear A
1067   D3 01                  OUT   (01),A   ;Deactivate Segment
1069   CB 08                  RRC   B   ;Move Segment Reference on to the Right
106B   30 ED                  JR   NC,SCAN_LOOP   ;If not passed the last segment, scan next segment
106D   D3 02                  OUT   (02),A   ;Clear port 2
106F   C9                     RET      
1070                             ;Convert HL and A to Seven Segment Display
1070                CON_HL_A:      
1070   01 00 50               LD   BC,DISP_BUFF   ;Location of display buffer
1073   7C                     LD   A,H   ;Get high byte of Address
1074   CD 7C 10               CALL   CON_A   ;Convert A to Segment Hex
1077   7D                     LD   A,L   ;Get low byte of Address
1078   CD 7C 10               CALL   CON_A   ;Convert A to Segment Hex
107B   7E                     LD   A,(HL)   ;Now get value at HL to convert (Data)
107C                CON_A:       
107C   F5                     PUSH   AF   ;Save A to keep original value
107D   07                     RLCA      ;Shift upper nibble to lower for masking
107E   07                     RLCA      
107F   07                     RLCA      
1080   07                     RLCA      
1081   CD 85 10               CALL   CON_NIBBLE   ;Convert Lower nibble to segment hex
1084   F1                     POP   AF   ;Restore A
1085                CON_NIBBLE:      
1085   E6 0F                  AND   0FH   ;Only look at lower nibble for indexing
1087   11 9F 10               LD   DE,DISP_COD_TAB   ;Reference Segment convert table
108A   83                     ADD   A,E   ;Index table with A
108B   5F                     LD   E,A   ;Update DE with index
108C   1A                     LD   A,(DE)   ;Look up table
108D   02                     LD   (BC),A   ;Save it to display buffer
108E   03                     INC   BC   ;Increment buffer location
108F   C9                     RET      
1090                KEY_TONE:      
1090   0E 40                  LD   C,40H   ;Half cycle count
1092   AF                     XOR   A   ;Clear A
1093                TONE_LOOP:      
1093   D3 01                  OUT   (01),A   ;Set or unset speaker bit
1095   06 40                  LD   B,40H   ;Set delay
1097   10 FE        TONE_DELAY:   DJNZ   TONE_DELAY   
1099   EE 80                  XOR   80H   ;Toggle speaker bit
109B   0D                     DEC   C   ;Count down cycle
109C   20 F5                  JR   NZ,TONE_LOOP   ;Do more toning
109E   C9                     RET      
109F                DISP_COD_TAB:      
109F   EB 28 CD AD 2E A7 E7 29 DB   0EBH,28H,0CDH,0ADH,2EH,0A7H,0E7H,29H   
10A7   EF AF 6F E6 C3 EC C7 47 DB   0EFH,0AFH,6FH,0E6H,0C3H,0ECH,0C7H,47H   
10AF                DISP_BUFF:   EQU   5000H   ;Display buffer


SETUP:              1000 DEFINED AT LINE 8
START:              1005 DEFINED AT LINE 13
                    > USED AT LINE 67
CHK_INP:            1018 DEFINED AT LINE 24
                    > USED AT LINE 72
FASTR:              1023 DEFINED AT LINE 31
                    > USED AT LINE 26
SLOWR:              102E DEFINED AT LINE 38
                    > USED AT LINE 33
STOP:               1039 DEFINED AT LINE 45
                    > USED AT LINE 40
SLOWF:              1040 DEFINED AT LINE 50
                    > USED AT LINE 47
DO_SCAN:            1041 DEFINED AT LINE 53
                    > USED AT LINE 29
                    > USED AT LINE 36
                    > USED AT LINE 43
                    > USED AT LINE 48
RESCAN:             1046 DEFINED AT LINE 60
                    > USED AT LINE 65
GET_PREV:           1051 DEFINED AT LINE 69
                    > USED AT LINE 20
SCAN:               1055 DEFINED AT LINE 74
                    > USED AT LINE 61
SCAN_LOOP:          105A DEFINED AT LINE 78
                    > USED AT LINE 90
D_LOOP:             1062 DEFINED AT LINE 84
                    > USED AT LINE 84
CON_HL_A:           1070 DEFINED AT LINE 95
                    > USED AT LINE 14
CON_A:              107C DEFINED AT LINE 103
                    > USED AT LINE 98
                    > USED AT LINE 100
CON_NIBBLE:         1085 DEFINED AT LINE 112
                    > USED AT LINE 109
KEY_TONE:           1090 DEFINED AT LINE 122
                    > USED AT LINE 15
TONE_LOOP:          1093 DEFINED AT LINE 125
                    > USED AT LINE 131
TONE_DELAY:         1097 DEFINED AT LINE 128
                    > USED AT LINE 128
DISP_COD_TAB:       109F DEFINED AT LINE 134
                    > USED AT LINE 114
DISP_BUFF:          5000 DEFINED AT LINE 138
                    > USED AT LINE 76
                    > USED AT LINE 96
